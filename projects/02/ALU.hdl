// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
  IN  
      x[16], y[16],  // 16-bit inputs        
      zx, // zero the x input?
      nx, // negate the x input?
      zy, // zero the y input?
      ny, // negate the y input?
      f,  // compute (out = x + y) or (out = x & y)?
      no; // negate the out output?
  OUT 
      out[16], // 16-bit output
      zr,      // if (out == 0) equals 1, else 0
      ng;      // if (out < 0)  equals 1, else 0

  PARTS:
  Mux16(a=x, b=false, sel=zx, out=x-zx);
  Not16(in=x-zx, out=not-x-zx);
  Mux16(a=x-zx, b=not-x-zx, sel=nx, out=x-nx);
  Mux16(a=y, b=false, sel=zy, out=y-zy);
  Not16(in=y-zy, out=not-y-zy);
  Mux16(a=y-zy, b=not-y-zy, sel=ny, out=y-ny);
  Add16(a=x-nx, b=y-ny, out=x-nx-plus-y-ny);
  And16(a=x-nx, b=y-ny, out=x-nx-and-y-ny);
  Mux16(a=x-nx-and-y-ny, b=x-nx-plus-y-ny, sel=f, out=x-y-f);
  Not16(in=x-y-f, out=not-x-y-f);
  Mux16(a=x-y-f, b=not-x-y-f, sel=no, out=out, out[0..7]=out-left, out[8..15]=out-right, out[15]=out-final);
  Or8Way(in=out-left, out=zr-left);
  Or8Way(in=out-right, out=zr-right);
  Or(a=zr-left, b=zr-right, out=not-zr);
  Not(in=not-zr, out=zr);
  And(a=true, b=out-final, out=ng);
}
